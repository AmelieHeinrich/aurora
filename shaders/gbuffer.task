#version 450

#extension GL_NV_mesh_shader : require
#extension GL_EXT_shader_8bit_storage : require
#extension GL_EXT_shader_16bit_storage : require
#extension GL_KHR_shader_subgroup_ballot : require

layout(local_size_x = 32, local_size_y = 1, local_size_z = 1) in;

const float pi_half = 1.57079;

struct Meshlet
{
	vec4 sphere;

	uint vertices[64];   
    uint indices_packed[124*3/4];   
    uint8_t vertex_count;
	uint8_t triangle_count;
};	

layout (binding = 1, set = 4) readonly buffer Meshlets 
{
	Meshlet meshlets[];
};

layout (binding = 0, set = 0) uniform Camera {
	mat4 projection;
	mat4 view;
	vec3 pos;
	float pad;
	vec4 frustrum_planes[6];
} camera;

layout (push_constant) uniform Model {
	mat4 transform;
} model;

out taskNV block
{
	uint meshletIndices[32];
};

bool InsideFrustum(vec4 sphere)
{
	for (int i = 0; i < 6; i++)
	{
		if (dot(camera.frustrum_planes[i].xyz, sphere.xyz) - camera.frustrum_planes[i].w <= -sphere.w)
			return false;
	}

	return true;
}

shared uint meshletCount;

void main()
{
	uint ti = gl_LocalInvocationID.x;
	uint mgi = gl_WorkGroupID.x;
	uint mi = mgi * 32 + ti;

	float scale_x = length(vec3(model.transform[0][0], model.transform[0][1], model.transform[0][2]));
	float scale_y = length(vec3(model.transform[1][0], model.transform[1][1], model.transform[1][2]));
	float scale_z = length(vec3(model.transform[2][0], model.transform[2][1], model.transform[2][2]));

	float mean_scale = (scale_x + scale_y + scale_z) / 3.0f;

	vec3 sphere_center = vec3(model.transform * vec4(meshlets[mi].sphere.xyz, 1.0));
	float sphere_radius = meshlets[mi].sphere.w * mean_scale;
	vec4 final_sphere = vec4(sphere_center, sphere_radius);

	bool accept = InsideFrustum(final_sphere);
	uvec4 ballot = subgroupBallot(accept);

	uint index = subgroupBallotExclusiveBitCount(ballot);

	if (accept)
		meshletIndices[index] = mi;

	uint count = subgroupBallotBitCount(ballot);

	if (ti == 0)
		gl_TaskCountNV = count;
}